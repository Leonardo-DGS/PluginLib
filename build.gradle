import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'net.leomixer17'
    version = '1.0.0'
    description = 'Library for a faster and easier Bukkit/Spigot and BungeeCord plugin development'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.compileJava.options.encoding = 'UTF-8'
    repositories {
        jcenter()
        mavenCentral()
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    }
    configurations.all {
        transitive = false
    }
    tasks.processResources {
        filter ReplaceTokens, tokens: [
                "version"    : project.property("version"),
                "description": project.property("description")
        ]
    }
}

dependencies {
    compile project(path: ':bukkit', configuration: 'shadow')
    compile project(path: ':bungee', configuration: 'shadow')
}

tasks.jar {
    enabled = false
    archiveFileName = 'PluginLib.jar'
}

tasks.shadowJar {
    archiveFileName = 'PluginLib.jar'
}

artifacts {
    archives shadowJar
}
